apply plugin: 'groovy'

repositories {
    jcenter()
}

dependencies {
    testCompile "com.google.guava:guava:23.5-jre"
    testCompile gradleTestKit()
    testCompile "junit:junit:4.12"
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4@jar"
}

def androidHome = file(System.getenv("ANDROID_HOME") ?: "${System.getProperty("user.home")}/Library/Android/sdk")
def checkoutDir = file("checkout/santa-tracker")
def originalDir = file("build/original")
def relocatedDir = file("build/relocated")

task processFileResources(type: Copy) {
    from "src/files/resources"
    into "build/files/resources"

    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
        ANDROID_HOME: androidHome.absolutePath
    ])
}

task cleanCheckout(type: Exec) {
    workingDir = checkoutDir
    commandLine = ["git", "clean", "-fdx"]
}

task copyOriginal(type: Copy) {
    dependsOn cleanCheckout

    from checkoutDir
    from processFileResources
    into originalDir
}

task copyRelocated(type: Copy) {
    dependsOn cleanCheckout

    from checkoutDir
    from processFileResources
    into relocatedDir
}

test {
    dependsOn copyOriginal
    dependsOn copyRelocated

    systemProperty "original.dir", originalDir.absolutePath
    systemProperty "relocated.dir", relocatedDir.absolutePath
    systemProperty "org.gradle.android.test.gradle-installation", System.getProperty("org.gradle.android.test.gradle-installation")
    systemProperty "org.gradle.android.test.android-version", System.getProperty("org.gradle.android.test.android-version")
    systemProperty "org.gradle.android.test.android-cache-fix-version", System.getProperty("org.gradle.android.test.android-cache-fix-version")
    systemProperty "org.gradle.android.test.scan-url", System.getProperty("org.gradle.android.test.scan-url")

    inputs.dir originalDir withPropertyName "original.dir"
    inputs.dir relocatedDir withPropertyName "relocated.dir"

    if (System.getenv("TRAVIS") == "true" || Boolean.getBoolean("org.gradle.android.test.show-output")) {
        testLogging.showStandardStreams = true
    }
}

assemble {
    dependsOn copyOriginal
    dependsOn copyRelocated
}
